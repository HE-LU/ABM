apply plugin: "java"
apply plugin: "idea"

repositories {
	jcenter()
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.miglayout:miglayout-swing:4.2'
	compile 'org.swinglabs:swingx:1.6.1'
	compile 'com.mashape.unirest:unirest-java:1.4.5'
	compile 'commons-io:commons-io:2.4'
	compile 'org.apache.httpcomponents:httpclient:4.4.1'
	compile 'org.apache.httpcomponents:httpasyncclient:4.1'
	compile 'org.apache.httpcomponents:httpmime:4.4.1'
	compile 'org.apache.httpcomponents:httpcore-nio:4.4.1'
	compile 'com.google.code.gson:gson:2.2.4'
	compile 'com.brsanthu:google-analytics-java:1.1.1'
	compile 'org.slf4j:slf4j-simple:1.7.10'
}

defaultTasks 'makeJar'


jar {
	baseName = 'gs-gradle'
	version = '0.1.0'
}

idea.project.ipr {
	beforeMerged {project -> project.modulePaths.clear()}
}

idea.module.iml {
	withXml {it.node.@type = "PLUGIN_MODULE"}
}

task setup {
	description = "crete .idea based project structure"
	dependsOn ideaModule, ideaProject
	doLast {
		copy {
			from '.'
			into '.idea/'
			include '*.ipr'
			rename {"modules.xml"}}
		project.delete "${project.name}.ipr"
	}
}


task makeJar(type: Jar, dependsOn: classes) {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	outputs.upToDateWhen {false}
	dependsOn setup

	from {
		configurations.compile.collect {
			(!it.isDirectory() && !it.getName().equals("openapi.jar") && !it.getName().equals("openapi-7.0.3.jar")) ? zipTree(it) : it
		}
	}

	from sourceSets.main.output

	doLast {
		copy {
			from './build/libs'
			into './'
			include '*.jar'
			rename {"ABM.jar"}
		}
		copy {
			from './build/libs'
			into './../.IdeaIC13/system/plugins-sandbox/plugins'
			include '*.jar'
			rename {"ABM.jar"}
		}
		delete "./build/libs/ABM.jar"
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.8'
}